AppType=StandardJava
Build1=Default,b4j.webapi
File1=help.html
File2=index.html
File3=main.html
FileGroup1=Default Group
FileGroup2=Default Group
FileGroup3=Default Group
Group=Default Group
Library1=byteconverter
Library2=jcore
Library3=jserver
Library4=json
Library5=jsql
Library6=javaobject
Module1=CorsFilter
Module2=DataConnector
Module3=DefaultHandler
Module4=HelpHandler
Module5=HomeHandler
Module6=HttpsFilter
Module7=ProductHandler
Module8=Utility
Module9=WebUtils
NumberOfFiles=3
NumberOfLibraries=6
NumberOfModules=9
Version=9.5
@EndOfDesignText@
' Name: 		Web API Template Lite
' Description: 	Non-UI application (console / server application)
' Version: 		1.12

'Click to create Publish.zip: ide://run?file=%JAVABIN%\jar.exe&WorkingDirectory=../Objects&Args=-cMf&Args=Publish.zip&Args=www&Args=*.jar&Args=*.ini&Args=*.db&Args=help.html&Args=help.js&Args=comments.txt

#If RELEASE
'#CustomBuildAction: 2, C:\Program Files\WinRAR\WinRAR.exe, a -afrar upload.rar www *.jar *.ini *.db help.html help.js -czcomments.txt
#CustomBuildAction: 2, C:\Program Files\WinRAR\WinRAR.exe, a upload.zip www *.jar *.ini *.db help.html help.js comments.txt
#End If
#Region Project Attributes 
	#CommandLineArgs:
	#MergeLibraries: True 
#End Region

#Region AdditionalJar
' MySQL connector
' #AdditionalJar: mysql-connector-java-5.1.49-bin
' SQLite connector
#AdditionalJar: sqlite-jdbc-3.36.0.2
#End Region

Sub Process_Globals
	Public const VERSION As String = "1.12"
	Public srvr As Server
	Public DB As DataConnector
	Public ROOT_PATH As String
	Public ROOT_URL As String
	Public SERVER_PORT As Int
	Public SSL_PORT As Int
	Public config As Map
	Public queries As Map
	Public Element As Element
	Type Element (Root As Int, Parent As Int, Parent_Id As Int, Child As Int, Child_Id As Int, Max_Elements As Int)
End Sub

Sub AppStart (Args() As String)
	config = Utility.ReadMapFile(File.DirApp, "config.ini")
	If config.Get("DbType").As(String).EqualsIgnoreCase("mysql") Then
		queries = Utility.ReadMapFile(File.DirApp, "queries-mysql.ini")
	End If
	If config.Get("DbType").As(String).EqualsIgnoreCase("sqlite") Then
		queries = Utility.ReadMapFile(File.DirApp, "queries-sqlite.ini")
	End If
	
	ROOT_PATH = config.Get("ROOT_PATH")
	ROOT_URL = config.Get("ROOT_URL")
	SERVER_PORT = config.Get("ServerPort")
	SSL_PORT = config.Get("SSLPort") : If IsNumber(SSL_PORT) = False Then SSL_PORT = 0
	
	If SERVER_PORT = 0 Then
		Log($"Server Port is not set!"$)
		Log($"Application is terminated."$)
		ExitApplication
	End If
	ROOT_URL = ROOT_URL & ":" & SERVER_PORT
	
	srvr.Initialize("")
	srvr.Port = SERVER_PORT
	
	If SSL_PORT > 0 Then
		ConfigureSSL(SSL_PORT)
		ROOT_URL = config.Get("ROOT_URL") & ":" & SSL_PORT
	End If
	' Update ROOT URL
	config.Put("ROOT_URL", ROOT_URL)
	config.Put("VERSION", VERSION)
	
	' Initialize a database connector object
	DB.Initialize

	Element.Initialize
	If ROOT_PATH <> "/" Then ' If webroot is using subdirectory
		srvr.AddHandler(ROOT_PATH, "HomeHandler", False)
		Element.Root = 1
		Element.Parent = 2
		Element.Parent_Id = 3
		Element.Child = 4
		Element.Child_Id = 5
		Element.Max_Elements = 6
	Else
		Element.Root = 0
		Element.Parent = 1
		Element.Parent_Id = 2
		Element.Child = 3
		Element.Child_Id = 4
		Element.Max_Elements = 5
	End If
	
	srvr.StaticFilesFolder = File.Combine(File.DirApp, "www")
	srvr.SetStaticFilesOptions(CreateMap("dirAllowed": False))
	
	' Add a home page
	srvr.AddHandler("", "HomeHandler", False)
	
	' Add a help page
	srvr.AddHandler(ROOT_PATH & "help/*", "HelpHandler", False)
	
	' Add more handlers here
	srvr.AddHandler(ROOT_PATH & "topic/*", "DefaultHandler", False)
	srvr.AddHandler(ROOT_PATH & "category/*", "ProductHandler", False)
	
	' Add CrossOriginFilter
	ConfigureCORS("/*", "*", "*", "*")
	'ConfigureCORS(ROOT_PATH & "category/*", "*", "", "")
	
	' Server starts
	srvr.Start
	
	Log($"Web API server (version = ${VERSION}) is running on port ${srvr.Port}"$)
	Log($"Open the following URL from your web browser"$)
	Log(ROOT_URL & ROOT_PATH)
	' Open a web browser and navigate to: http://127.0.0.1:19800/v1/
	StartMessageLoop
End Sub

#Region ConfigureSSL
Private Sub ConfigureSSL (SslPort As Int)
	Dim KeyStoreDir As String = config.Get("SSL_KEYSTORE_DIR")
	Dim KeyStoreFile As String = config.Get("SSL_KEYSTORE_FILE")
	Dim KeyStorePassword As String = config.Get("SSL_KEYSTORE_PASSWORD")
	Dim ssl As SslConfiguration
	ssl.Initialize
	ssl.SetKeyStorePath(KeyStoreDir, KeyStoreFile)
	ssl.KeyStorePassword = KeyStorePassword
	'ssl.KeyManagerPassword = ""
	srvr.SetSslConfiguration(ssl, SslPort)
	'add filter to redirect all traffic from http to https (optional)
	srvr.AddFilter("/*", "HttpsFilter", False)
End Sub
#End Region

#Region ConfigureCORS
	' allowedOrigins = "*" or "http://google.com"
	' allowedMethods = "*" or "GET,POST,HEAD"
	' allowedHeaders = "*" or "X-Requested-With,Content-Type,Accept,Origin"
Private Sub ConfigureCORS (Path As String, allowedOrigins As String, allowedMethods As String, allowedHeaders As String)
	' Reference: https://www.b4x.com/android/forum/threads/jetty-cross-origin-filter-to-be-added-to-jserver-library.85641/
	Dim cors As CorsFilter
	cors.Initialize(Path, CreateMap("allowedOrigins": allowedOrigins, _
	"allowedMethods": allowedMethods, _
	"allowedHeaders": allowedHeaders, _
	"allowCredentials": "true", _
	"preflightMaxAge": 1800, _
	"chainPreflight": "false"))
	cors.AddToServer(srvr)
End Sub
#End Region